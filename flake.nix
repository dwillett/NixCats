# NixCats: flake.nix
{
  description = "dwillett's NixCats-nvim configuration";

  inputs = {
    # ----- System Flakes ----- #
    # Nixpkgs
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";

    # Neovim nightly overlay
    neovim-nightly-overlay.url = "github:nix-community/neovim-nightly-overlay";

    # Nixcats
    nixCats.url = "github:BirdeeHub/nixCats-nvim";

    # ----- External Plugins ----- #
    plugins-shadowenv-vim = {
      url = "git+https://github.com/Shopify/shadowenv.vim";
      flake = false;
    };
    plugins-nvim-dap-ruby = {
      url = "github:suketa/nvim-dap-ruby";
      flake = false;
    };
    plugins-neotest-ruby-minitest = {
      url = "github:dwillett/neotest-ruby-minitest";
      # url = "git+file:///Users/willett/src/github.com/dwillett/neotest-ruby-minitest";
      flake = false;
    };
    plugins-ergoterm-nvim = {
      url = "github:waiting-for-dev/ergoterm.nvim";
      flake = false;
    };
    plugins-lualine-nvim = {
      url = "github:dwillett/lualine.nvim/git-branch-reftable-support";
      flake = false;
    };
    plugins-canopy-nvim = {
      url = "git+https://github.com/shopify-playground/canopy.nvim";
      # url = "git+file:///Users/willett/src/github.com/dwillett/canopy.nvim";
      flake = false;
    };
  };

  outputs = {
    nixpkgs,
    nixCats,
    ...
  } @ inputs: let
    # Make utils more accessible
    inherit (nixCats) utils;

    # Path for lua file entry is here
    luaPath = "${./.}";

    # Generate config for all systems
    forEachSystem = utils.eachSystem nixpkgs.lib.platforms.all;

    # Grab nonfree packages
    extra_pkg_config = {
      allowUnfree = true;
    };

    # Apply overlays
    dependencyOverlays = [
      # Grab all inputs named `plugins-<pluginName>`
      (utils.standardPluginOverlay inputs)
      # Apply nightly overlay to pull from neovim-nightly
      # inputs.neovim-nightly-overlay.overlays.default
    ];

    # Category definitions are here; this variable should be a function!
    categoryDefinitions = import ./categoryDefinitions.nix {
      inherit inputs;
    };

    # Package definitions are here, this should be an attrset
    packageDefinitions = import ./packageDefinitions.nix {
      inherit inputs nixpkgs utils;
    };

    # The default package to use from packageDefinitions
    defaultPackageName = "nixCats";
  in
    forEachSystem (system: let
      # The builder function
      nixCatsBuilder =
        utils.baseBuilder luaPath {
          inherit
            system
            dependencyOverlays
            extra_pkg_config
            nixpkgs
            ;
        }
        categoryDefinitions
        packageDefinitions;

      # Setting the default package
      defaultPackage = nixCatsBuilder defaultPackageName;

      # For using utils, such as pkgs.mkShell in the outputs
      pkgs = import nixpkgs {inherit system;};
    in {
      # this will make a package out of each of the packageDefinitions defined above
      # and set the default package to the one passed in here.
      packages = utils.mkAllWithDefault defaultPackage;

      # The dev-shell
      # and add whatever else you want in it.
      devShells = import ./shell.nix {
        packages = utils.mkAllWithDefault defaultPackage;
        inherit pkgs defaultPackageName defaultPackage;
      };
    })
    // (let
      # We also export a nixos module to allow reconfiguration from configuration.nix
      # and the same for home manager
      inheritVars = {
        moduleNamespace = [defaultPackageName];
        inherit
          defaultPackageName
          dependencyOverlays
          luaPath
          categoryDefinitions
          packageDefinitions
          extra_pkg_config
          nixpkgs
          ;
      };
      nixosModule = utils.mkNixosModules inheritVars;
      homeManagerModule = utils.mkHomeModules inheritVars;
    in {
      # these outputs will be NOT wrapped with ${system}

      # This will make an overlay out of each of the packageDefinitions defined above
      # and set the default overlay to the one named here.
      overlays =
        utils.makeOverlays luaPath {
          # we pass in the things to make a pkgs variable to build nvim with later
          inherit nixpkgs dependencyOverlays extra_pkg_config;
          # and also our categoryDefinitions
        }
        categoryDefinitions
        packageDefinitions
        defaultPackageName;

      nixosModules.default = nixosModule;
      homeManagerModules.default = homeManagerModule;

      inherit utils nixosModule homeManagerModule;
      inherit (utils) templates;
    });
}
